/*
 * Copyright 2011-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// ------------- compiling demo source ---------------

sourceSets {
    demo {
        compileClasspath += sourceSets.main.output + configurations.compile + configurations.javafx
        runtimeClasspath += sourceSets.main.output + configurations.javafx
        groovy {
            srcDir 'src/demo/groovy'
        }
        resources {
            srcDir 'src/demo/resources'
        }
    }
}

// ---------------- dynamic tasks to run demos ------------------

new File('src/demo/groovy').eachFileMatch(~/.*Demo.groovy/) { file ->
    def taskName = file.name - '.groovy'
    task "$taskName"(type: JavaExec) {
        group = "Demo"
        description = "Run the $taskName program"
        main = taskName
        classpath sourceSets.main.runtimeClasspath + sourceSets.demo.runtimeClasspath
    }
}


/*
 * Copyright 2011-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.8.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
        classpath 'net.nemerosa:versioning:2.7.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.0'
    }
}

ext {
    buildTimeAndDate = new Date()
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'net.nemerosa.versioning'
apply plugin: 'com.google.osdetector'

ext.platform = osdetector.os != 'osx' ? osdetector.os : 'mac'

repositories {
    if (project.hasProperty('groovyfx_useMavenLocal') && Boolean.valueOf(project.groovyfx_useMavenLocal)) { mavenLocal() }
    jcenter()
}

configurations {
    jansi.extendsFrom(runtime)
    javafx
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyfx_groovyVersion}"
    testCompile "junit:junit:${groovyfx_junitVersion}"
    jansi "org.fusesource.jansi:jansi:${groovyfx_jansiVersion}"
    testCompile("org.spockframework:spock-core:${groovyfx_spockVersion}") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }

    // TODO refactor duplication

    compileOnly "org.openjfx:javafx-base:${groovyfx_javafxVersion}:${platform}"
    compileOnly "org.openjfx:javafx-graphics:${groovyfx_javafxVersion}:${platform}"
    compileOnly "org.openjfx:javafx-controls:${groovyfx_javafxVersion}:${platform}"
    compileOnly "org.openjfx:javafx-fxml:${groovyfx_javafxVersion}:${platform}"
    compileOnly "org.openjfx:javafx-swing:${groovyfx_javafxVersion}:${platform}"
    compileOnly "org.openjfx:javafx-web:${groovyfx_javafxVersion}:${platform}"

    testCompileOnly "org.openjfx:javafx-base:${groovyfx_javafxVersion}:${platform}"
    testCompileOnly "org.openjfx:javafx-graphics:${groovyfx_javafxVersion}:${platform}"
    testCompileOnly "org.openjfx:javafx-controls:${groovyfx_javafxVersion}:${platform}"
    testCompileOnly "org.openjfx:javafx-fxml:${groovyfx_javafxVersion}:${platform}"
    testCompileOnly "org.openjfx:javafx-swing:${groovyfx_javafxVersion}:${platform}"
    testCompileOnly "org.openjfx:javafx-web:${groovyfx_javafxVersion}:${platform}"

    javafx "org.openjfx:javafx-base:${groovyfx_javafxVersion}:${platform}"
    javafx "org.openjfx:javafx-graphics:${groovyfx_javafxVersion}:${platform}"
    javafx "org.openjfx:javafx-controls:${groovyfx_javafxVersion}:${platform}"
    javafx "org.openjfx:javafx-fxml:${groovyfx_javafxVersion}:${platform}"
    javafx "org.openjfx:javafx-swing:${groovyfx_javafxVersion}:${platform}"
    javafx "org.openjfx:javafx-web:${groovyfx_javafxVersion}:${platform}"
}

apply {
    from 'gradle/demo.gradle'
    from 'gradle/deploy.gradle'
    from 'gradle/publishing.gradle'
    from 'gradle/code-quality.gradle'
}

jar {
    manifest {
        attributes(
            'X-Built-By': System.properties['user.name'],
            'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
            'X-Build-Date': buildDate,
            'X-Build-Time': buildTime,
            'X-Build-Revision': versioning.info.commit,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Specification-Vendor': 'groovyfx.org',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'groovyfx.org'
        )
    }

    metaInf {
        from '.'
        include 'LICENSE'
    }
}

idea {
  module {
    // excludeDirs += file('gradle/') // Gradle directory including the wrapper subdirectory.
    excludeDirs += file('.settings/') // Eclipse settings directory.
    excludeDirs += file('bin') // Eclipse compilation directory.
    excludeDirs += file('out') // IDEA compilation directory.
    excludeDirs += file('build') // Gradle compilation directory.
  }
  project {
    ipr {
      withXml { provider ->
        final node = provider.asNode()
        final component = provider.asNode().component
        node.component.find { it.'@name' == 'VcsDirectoryMappings' }.mapping[0].'@vcs' = 'Git'
        final gradleSettings = node.appendNode('component' , [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'linkedProjectPath', value: '$PROJECT_DIR$/build.gradle'])
      }
    }
  }
}

asciidoctor {
  //sources { include 'index.adoc' }
  attributes toc: 'left',
             doctype: 'book',
             icons: 'font',
             encoding: 'utf-8',
             sectlink: true,
             sectanchors: true,
             numbered: true,
             linkattrs: true,
             imagesdir: 'images',
             stylesheet: 'css/style.css',
             'source-highlighter': 'coderay',
             'groovyfx-version': project.version,
             'groovy-version': project.groovyfx_groovyVersion
}

test {
    testLogging {
        exceptionFormat "full"
    }
}

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = project.files(sourceSets.main.allSource.srcDirs)
    sourceDirectories = project.files(sourceSets.main.allSource.srcDirs)
    classDirectories =  project.files(sourceSets.main.output)
    reports {
        xml.enabled = true
        csv.enabled = false
        html.enabled = true
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            'groovyfx.version'   : project.version
        ])
    }
}
